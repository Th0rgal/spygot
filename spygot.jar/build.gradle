import java.text.SimpleDateFormat

plugins {
    id "java"
    id "idea"
    id "eclipse"
    id "maven-publish"
    id "com.github.johnrengelman.shadow" version "6.1.0"
}

group "io.th0rgal"
def pluginVersion = "0.0.0"
version = pluginVersion

repositories {
    mavenCentral()
    mavenLocal()
    //Spigot
    maven { url "https://hub.spigotmc.org/nexus/content/repositories/snapshots/" }
    //Because Spigot depends on Bungeecord ChatComponent-API
    maven { url "https://oss.sonatype.org/content/repositories/snapshots" }
    //JitPack
    maven { url 'https://jitpack.io' }
    //ProtocolLib
    maven { url "https://repo.dmulloy2.net/nexus/repository/public/" }
    //Minecraft repo (commodore)
    maven { url "https://libraries.minecraft.net/" }
    //MythicMobs
    maven { url "https://mvn.lumine.io/repository/maven-public/" }
    //PlaceHolderAPI
    maven { url 'https://repo.extendedclip.com/content/repositories/placeholderapi/' }
    //EffectLib
    maven { url 'https://maven.elmakers.com/repository/' }
    //sk89q-repo
    maven { url 'https://maven.enginehub.org/repo/' }
    //CodeMc (bstats)
    maven { url "https://repo.codemc.org/repository/maven-public" }
    jcenter() // needed for building shadow from protocolLib 4.5.0
}

compileJava {
    options.compilerArgs += ["-parameters"]
    options.fork = true
    options.forkOptions.executable = 'javac'
    options.encoding = "UTF-8"
}

tasks.withType(JavaCompile) {
    options.compilerArgs << '-Xlint:unchecked'
    options.deprecation = true
}

dependencies {
    // The Spigot API with no shadowing. Requires the OSS repo.
    compileOnly group: "org.spigotmc", name: "spigot", version: "1.16.4-R0.1-SNAPSHOT"
    compileOnly group: "org.spigotmc", name: "spigot-api", version: "1.16.5-R0.1-SNAPSHOT"
    implementation group: 'net.sf.py4j', name: 'py4j', version: '0.10.9.1'
    implementation group: 'org.yaml', name: 'snakeyaml', version: '1.27'
}

processResources {
    filesNotMatching(["**/*.png", "**/models/**", "**/textures/**"]) {
        expand projectVersion: pluginVersion
    }
}

java {
    withJavadocJar()
    withSourcesJar()
}

shadowJar {
    relocate "net.sf.py4j", "io.th0rgal.spygot.api.py4j"
    minimize {
        exclude("io.th0rgal.spygot.api.api.*")
    }

    manifest {
        attributes(
                'Built-By': System.properties['user.name'],
                'Version': pluginVersion,
                'Build-Timestamp': new SimpleDateFormat("yyyy-MM-dd' 'HH:mm:ss.SSSZ").format(new Date()),
                'Created-By': "Gradle ${gradle.gradleVersion}",
                'Build-Jdk': "${System.properties['java.version']} (${System.properties['java.vendor']} ${System.properties['java.vm.version']})",
                'Build-OS': "${System.properties['os.name']} ${System.properties['os.arch']} ${System.properties['os.version']}"
        )
    }
}

compileJava.dependsOn clean
build.dependsOn shadowJar
